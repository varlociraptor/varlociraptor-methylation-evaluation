# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.


configfile: "config/config.yaml"


read_type = config["read_type"]
genome_config = config["sample"]
accession = config["accession"]


aligned_bam_reads = (
    "resources/aligned-reads-pacbio.bam"
    if read_type == "PacBio"
    else "resources/aligned-reads-illumina.bam"
)

aligned_bam_reads_index = (
    "resources/aligned-reads-pacbio-sorted.bam.bai"
    if read_type == "PacBio"
    else "resources/aligned-reads-illumina-sorted.bam.bai"
)

aligned_sam_reads = (
    "resources/aligned-reads-pacbio.sam"
    if read_type == "PacBio"
    else "resources/aligned-reads-illumina.sam"
)

aligned_bam_reads_sorted = (
    "resources/aligned-reads-pacbio-sorted.bam"
    if read_type == "PacBio"
    else "resources/aligned-reads-illumina-sorted.bam"
)

aligned_sam_reads_sorted = (
    "resources/aligned-reads-pacbio-sorted.sam"
    if read_type == "PacBio"
    else "resources/aligned-reads-illumina-sorted.sam"
)


rule all:
    input:
        dv="results/scatter_plot_dv.png",
        tv="results/scatter_plot_tv.png",
        td="results/scatter_plot_td.png"


rule get_genome:
    output:
        "resources/genome.fasta",
    params:
        species=genome_config["species"],
        datatype=genome_config["datatype"],
        build=genome_config["build"],
        release=genome_config["release"],
    log:
        "logs/get_genome.log",
    cache: "omit-software"  # save space and time with between workflow caching (see docs)
    wrapper:
        "v2.3.2/bio/reference/ensembl-sequence"


rule get_chromosome:
    output:
        "resources/chr1.fasta",
    params:
        species=genome_config["species"],
        datatype=genome_config["datatype"],
        build=genome_config["build"],
        release=genome_config["release"],
        chromosome=genome_config["chromosome"],
    log:
        "logs/get_chromosome.log",
    cache: "omit-software"  # save space and time with between workflow caching (see docs)
    wrapper:
        "v2.3.2/bio/reference/ensembl-sequence"


rule genome_index:
    input:
        "resources/genome.fasta",
    output:
        "resources/genome.fasta.fai",
    log:
        "logs/genome_index.log",
    conda:
        "envs/samtools.yaml"
    params:
        pipeline_path=config["pipeline_path"],
    shell:
        """ 
        samtools faidx {params.pipeline_path}{input}
        """


rule find_candidates:
    input:
        "resources/genome.fasta",
    output:
        "resources/candidates.bcf",
    log:
        "logs/find_candidates.log",
    conda:
        "envs/varlociraptor.yaml"
    params:
        varlo_path=config["varlo_path"],
        pipeline_path=config["pipeline_path"],
    shell:
        """ 
        cd {params.varlo_path}
        cargo run -- methylation-candidates {params.pipeline_path}{input} {params.pipeline_path}{output}
        """

# Unused
rule candidates_to_vcf:
    input:
        "resources/candidates.bcf",
    output:
        "resources/candidates.vcf",
    conda:
        "envs/samtools.yaml"
    log:
        "logs/convert_to_vcf.log",
    shell:
        """
        bcftools view {input} > {output}
        """




rule get_fastq_pe:
    output:
        # the wildcard name must be accession, pointing to an SRA number
        "resources/{accession}_1.fastq",
        "resources/{accession}_2.fastq",
    log:
        "logs/pe/{accession}.log"
    params:
        extra="--skip-technical"
    threads: 6  # defaults to 6
    conda:
        "envs/fastq-wrapper.yaml"
    wrapper:
        "v2.6.0/bio/sra-tools/fasterq-dump"


rule align_reads:
    input:
        fasta="resources/genome.fasta",
        reads1=expand("resources/{accession}_1.fastq", accession=accession),
        reads2=expand("resources/{accession}_2.fastq", accession=accession),
    output:
        aligned_bam_reads,
    conda:
        "envs/bwa-meth.yaml"
    log:
        "logs/align_reads.log",
    shell:
        """
        bwameth.py index-mem2 {input.fasta}
        bwameth.py --threads 10 --reference {input.fasta} {input.reads1} {input.reads2}  | samtools view -S -b - > {output}
        """


rule aligned_reads_sorted_sam:
    input:
        aligned_bam_reads,
    output:
        sam_reads=aligned_sam_reads,
        sam_sorted=aligned_sam_reads_sorted,
    log:
        "logs/aligned_reads_sorted_sam.log",
    conda:
        "envs/samtools.yaml"
    params:
        pipeline_path=config["pipeline_path"],
    shell:
        """ 
        samtools view -bS {params.pipeline_path}{input} > {params.pipeline_path}/{output.sam_reads}    
        samtools sort {output.sam_reads} -o {output.sam_sorted}    
        """

rule sort_aligned_reads:
    input:
        aligned_bam_reads,
    output:
        aligned_bam_reads_sorted,
    log:
        "logs/sort_aligned_reads.log",
    conda:
        "envs/samtools.yaml"
    params:
        pipeline_path=config["pipeline_path"],
    shell:
        """
        samtools sort {input} -o {output}    
        """

rule aligned_reads_index:
    input:
        aligned_bam_reads_sorted,
    output:
        aligned_bam_reads_index,
    log:
        "logs/aligned_reads_to_bam.log",
    conda:
        "envs/samtools.yaml"
    params:
        pipeline_path=config["pipeline_path"],
    shell:
        """
        samtools index {params.pipeline_path}/{input}
        """

rule methylDackel:
    input:
        genome="resources/example_new/genome.fasta",
        alignment=aligned_bam_reads_sorted,
        alignment_index=aligned_bam_reads_index
    output:
        "results/alignments_CpG.bedGraph",
    conda:
        "envs/methylDackel.yaml"
    log:
        "logs/methylDackel.log",
    params:
        pipeline_path=config["pipeline_path"],
    shell:
        """
        MethylDackel extract {input.genome} {input.alignment} -o {params.pipeline_path}/results/alignments --mergeContext
        """

rule compute_meth_observations:
    input:
        genome="resources/genome.fasta",
        genome_index="resources/genome.fasta.fai",
        alignments=aligned_bam_reads_sorted,
        alignments_index=aligned_bam_reads_index,
        candidates="resources/candidates.bcf",
    output:
        "results/normal.bcf",
    log:
        "logs/copute.log",
    conda:
        "envs/varlociraptor.yaml"
    params:
        varlo_path=config["varlo_path"],
        pipeline_path=config["pipeline_path"],
        read_type=config["read_type"],
    shell:
        """ 
        cd {params.varlo_path}
        cargo run -- preprocess variants {params.pipeline_path}{input.genome} --candidates {params.pipeline_path}{input.candidates} --bam {params.pipeline_path}{input.alignments} --read-type {params.read_type} > {params.pipeline_path}{output}
        """

# Unused
rule observations_to_vcf:
    input:
        "results/normal.bcf",
    output:
        "results/normal.vcf",
    conda:
        "envs/samtools.yaml"
    log:
        "logs/convert_to_vcf.log",
    shell:
        """
        bcftools view {input} > {output}
        """


rule call_methylation:
    input:
        preprocess_obs="results/normal.bcf",
        scenario="resources/scenario.yaml",
    output:
        "results/calls.bcf",
    log:
        "logs/copute.log",
    conda:
        "envs/varlociraptor.yaml"
    params:
        varlo_path=config["varlo_path"],
        pipeline_path=config["pipeline_path"],
        read_type=config["read_type"],
    shell:
        """ 
        cd {params.varlo_path}
        cargo run -- call variants --omit-strand-bias generic --scenario {params.pipeline_path}{input.scenario} --obs normal={params.pipeline_path}{input.preprocess_obs} > {params.pipeline_path}/{output}
        """


rule calls_to_vcf:
    input:
        "results/calls.bcf",
    output:
        "results/calls.vcf",
    conda:
        "envs/samtools.yaml"
    log:
        "logs/convert_to_vcf.log",
    shell:
        """
        bcftools view {input} -o {output}
        """

rule download_bedGraphs:
    output:
        "resources/HG002/{bedGraph}.bedGraph.gz",
    log:
        "logs/download_bedGraphs{bedGraph}.log",
    params:
        pipeline_path=config["pipeline_path"],
        bedGraphs = config["bedGraphs_HG002"]
    script:
        "scripts/get_bedGraph_data.py"


rule process_data:
    input:
        "resources/HG002/{bedGraph}.bedGraph.gz"
    output:
        "resources/HG002/{bedGraph}.bedGraph"
    log:
        "logs/process_data{bedGraph}.log",
    shell:
        "gunzip -c {input} > {output}"


rule compute_avg_bedGraph:
    input:
        # expand("resources/HG002/bedGraph/{bed}.bedGraph", bed=bedGraphs),
        expand("resources/HG002/{bedGraph}.bedGraph", bedGraph=config["bedGraphs_HG002"])

    output:
        "resources/bed_avg.bedGraph",
    log:
        "logs/compute_avg_bedGraph.log",
    script:
        "scripts/compute_avg_bedGraph.py"


rule plot_results:
    input:
        bedGraph="results/alignments_CpG.bedGraph",
        calls="results/calls.vcf",
        # true_meth="resources/genome_se_f150r150_dir.ch3"
        true_meth="resources/bed_avg.bedGraph"
    output:
        dv="results/scatter_plot_dv.png",
        tv="results/scatter_plot_tv.png",
        td="results/scatter_plot_td.png"
    conda:
        "envs/altair.yaml"
    log:
        "logs/plot_results.log",
    script:
        "scripts/scatter_plot.py"
